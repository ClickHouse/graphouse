buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

project.repositories {
    mavenCentral()
    mavenLocal()
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

group = 'ru.yandex'
version = '1.1-SNAPSHOT'

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

def springVersion = '4.3.12.RELEASE'
def log4jVersion = '2.10.0'
def jettyVersion = '9.4.7.v20170914'

dependencies {
    compile 'ru.yandex.clickhouse:clickhouse-jdbc:0.1.33'
    compile 'com.google.guava:guava:23.0'
    compile 'com.google.code.gson:gson:2.8.2'
    compile "org.springframework:spring-jdbc:$springVersion"
    compile "org.springframework:spring-context:$springVersion"
    compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-web:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    compile 'commons-dbcp:commons-dbcp:1.4'
    compile "org.eclipse.jetty:jetty-server:$jettyVersion"
    compile "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    compile 'com.beust:jcommander:1.60'
    compile 'org.jparsec:jparsec:3.0'
    compile 'xerces:xercesImpl:2.11.0'

    testCompile 'junit:junit:4.12'
    testCompile 'com.mockrunner:mockrunner-jdbc:1.1.2'
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile 'org.testcontainers:testcontainers:1.5.1'
}

project.sourceSets {
    script {
        resources {
            srcDir 'src/main/script'
        }
    }
}

mainClassName = 'ru.yandex.market.graphouse.GraphouseMain'

task insertPerfTest(type: CreateStartScripts) {
    mainClassName = 'ru.yandex.market.graphouse.perf.InsertTest'
    applicationName = 'inputPerfTest'
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
    applicationDefaultJvmArgs = [
        '-Xmx1g',
    ]
    doLast {
        delete windowsScript
    }
}

applicationDistribution.into("bin") {
    from(insertPerfTest)
    fileMode = 0755
}

distributions {
    main {
        contents {
            from('src/main/script/log4j2.xml') {
                into 'conf'
            }
            from('src/main/script/graphouse.vmoptions') {
                into 'conf'
            }
            from('src/main/resources/graphouse-default.properties') {
                rename('graphouse-default.properties', 'graphouse.properties')
                into 'conf'
            }
            from('src/main/pySources/graphouse.py') {
                into 'bin'
            }
            from('src/main/pySources/graphouse_api.py') {
                into 'bin'
            }
            from('LICENSE') {
                into ''
            }
            //Creating empty dir
            into('') {
                def logDirBase = new File("$buildDir/tmp/logDirBase")
                new File(logDirBase.absolutePath + '/log').mkdirs()
                from { logDirBase }
            }
        }
    }
}

applicationDefaultJvmArgs = [
    '-Dapp.home=GRAPHOUSE_APP_HOME',
    '-Dlog4j.configurationFile=GRAPHOUSE_APP_HOME/conf/log4j2.xml',
    '-verbose:gc', '-XX:+PrintGC', '-XX:+PrintGCDetails', '-XX:+PrintGCTimeStamps', '-XX:+PrintGCDateStamps',
    '-XX:+UseGCLogFileRotation', '-XX:NumberOfGCLogFiles=7', '-XX:GCLogFileSize=50M',
    '-Xloggc:GRAPHOUSE_APP_HOME/log/graphouse.gc.log',
    '-XX:+HeapDumpOnOutOfMemoryError', '-XX:HeapDumpPath=GRAPHOUSE_APP_HOME/log/',
    '-showversion', '-server', '-XX:+UseCompressedOops',
    '-Dsun.net.inetaddr.ttl=60', '-Dsun.net.inetaddr.negative.ttl=0'
]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('GRAPHOUSE_APP_HOME', '\$APP_HOME')
        unixScript.text = unixScript.text.replace(
            'APP_HOME="`pwd -P`"',
            'APP_HOME="`pwd -P`"\n' +
                'GRAPHOUSE_OPTS=$(<$APP_HOME/conf/graphouse.vmoptions)'
        )
        delete windowsScript
    }
}